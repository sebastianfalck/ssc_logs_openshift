pipeline {
    agent any

    environment {
        REPO_URL         = ''
        REPO_CREDENTIALS = 'github-credentials'
        SERVER_INTERNAL  = 'https://internal.openshift.example:6443'
        SERVER_EXTERNAL  = 'https://external.openshift.example:6443'
        SERVER_DRS       = 'https://drs.openshift.example:6443'
        SENSITIVE_WORDS  = 'password,username,user,token,secret'
    }

    parameters {
        string(name: 'NOMBRE',    defaultValue: '',                   description: 'Nombre del microservicio')
        choice(name: 'AMBIENTE',  choices: ['dev','uat','prd','drs'], description: 'Ambiente')
        choice(name: 'ACCION',
               choices: ['none','get logs','get deployment','describe pod','get quota','get pods','get secrets','get configmaps'],
               description: 'Comando a ejecutar')
    }

    stages {
        stage('Checkout Project') {
            steps {
                dir('project') {
                    git branch: 'main',
                        url: env.REPO_URL,
                        credentialsId: env.REPO_CREDENTIALS
                }
            }
        }

        stage('Leer configuración CSV') {
            steps {
                script {
                    def lines   = readFile('project/ProjectsJenkinsCardifCSV.csv')
                                         .readLines()
                                         .findAll { it.trim() }
                    def headers = lines[0].split(';').collect { it.trim() }
                    def fila = lines.drop(1)
                        .collect { l ->
                            def vals = l.split(';')
                            def m    = [:]
                            headers.eachWithIndex { h,i -> m[h] = (vals.size()>i?vals[i]:'').trim() }
                            m
                        }
                        .find { it.appName == params.NOMBRE }
                    writeFile file: "fila_${params.NOMBRE}.json",
                              text: groovy.json.JsonOutput.toJson(fila)
                }
            }
        }

        stage('Confirmación de país') {
            steps {
                script {
                    def fila    = readJSON file: "fila_${params.NOMBRE}.json"
                    def paisCsv = fila.country?.toLowerCase()
                    def partes  = env.WORKSPACE.split(/[\\/]+/)
                    if (partes.size()<2) error "Ruta demasiado corta: ${env.WORKSPACE}"
                    def paisRuta = partes[-2].toLowerCase()
                    echo "País ruta: ${paisRuta}, país CSV: ${paisCsv}"
                    if (paisCsv != paisRuta) {
                        error "El microservicio no pertenece a tu país: ${paisCsv} vs ${paisRuta}"
                    }
                }
            }
        }

        stage('Ejecutar Acción') {
            when { expression { params.ACCION != 'none' } }
            steps {
                script {
                    def fila      = readJSON file: "fila_${params.NOMBRE}.json"
                    def utils     = new JenkinsUtils(this)
                    def server    = utils.getServer(params.AMBIENTE, fila.usage)
                    def namespace = fila["NameSpace${params.AMBIENTE.capitalize()}"]

                    def sanitize = { text ->
                        def sensitiveWords = env.SENSITIVE_WORDS.split(',').collect { it.trim().toLowerCase() }
                        def sanitizedLines = text.readLines().collect { line ->
                            def matcherEq = line =~ /^\s*([a-zA-Z0-9._-]+)\s*=\s*(.*)$/
                            if (matcherEq.matches()) {
                                def key = matcherEq[0][1]
                                if (sensitiveWords.any { key.toLowerCase().contains(it) }) {
                                    return "${key}=****"
                                }
                            }
                            def matcherColon = line =~ /^\s*([a-zA-Z0-9._-]+)\s*:\s*(.*)$/
                            if (matcherColon.matches()) {
                                def key = matcherColon[0][1]
                                if (sensitiveWords.any { key.toLowerCase().contains(it) }) {
                                    return "${key}: ****"
                                }
                            }
                            def matcherXml = line =~ /<entry key=["']([^"']+)["']>([^<]*)<\/entry>/
                            if (matcherXml.matches()) {
                                def key = matcherXml[0][1]
                                if (sensitiveWords.any { key.toLowerCase().contains(it) }) {
                                    return line.replaceFirst(/>([^<]*)<\/entry>/, '>****</entry>')
                                }
                            }
                            return line
                        }
                        return sanitizedLines.join('\n')
                    }

                    wrap([$class: 'MaskPasswordsBuildWrapper',
                          varPasswordPairs: [[var: 'TOKEN', password: fila["Token${params.AMBIENTE.capitalize()}"]]]]) {
                        sh utils.getOcLoginCmd(server, env.TOKEN)

                        def salida

                        switch (params.ACCION) {
                            case 'get logs':
                                def pod = utils.getPodName(namespace, params.NOMBRE)
                                echo "Pod: ${pod}"
                                utils.getLogs(pod, namespace, params.NOMBRE, params.AMBIENTE)
                                salida = readFile('reporte.html')
                                break
                            case 'get deployment':
                                def deployment = utils.getDeploymentName(namespace, params.NOMBRE)
                                echo "Deployment: ${deployment}"
                                utils.getDeploymentYaml(deployment, namespace, params.NOMBRE, params.AMBIENTE)
                                salida = readFile('reporte.html')
                                break
                            case 'describe pod':
                                def pod = utils.getPodName(namespace, params.NOMBRE)
                                echo "Pod: ${pod}"
                                utils.getDescribePod(pod, namespace, params.NOMBRE, params.AMBIENTE)
                                salida = readFile('reporte.html')
                                break
                            case 'get quota':
                                utils.getQuota(namespace, params.NOMBRE, params.AMBIENTE)
                                salida = readFile('reporte.html')
                                break
                            case 'get pods':
                                utils.getPods(namespace, params.NOMBRE, params.AMBIENTE)
                                salida = readFile('reporte.html')
                                break
                            case 'get secrets':
                                if (['qa', 'prd'].contains(params.AMBIENTE)) {
                                    input message: "¿Aprobar acceso a secrets de ${params.AMBIENTE} para el microservicio ${params.NOMBRE}?",
                                          ok: 'Aprobar',
                                          submitter: 'admin1,admin2'
                                    utils.extractSecret(namespace, params.NOMBRE, params.AMBIENTE)
                                    salida = sanitize(readFile('reporte.html'))
                                } else {
                                    // dev o drs no requieren aprobación ni censura
                                    utils.extractSecret(namespace, params.NOMBRE, params.AMBIENTE)
                                    salida = readFile('reporte.html')
                                }
                                break
                            case 'get configmaps':
                                utils.extractConfigMap(namespace, params.NOMBRE, params.AMBIENTE)
                                salida = sanitize(readFile('reporte.html'))
                                break
                        }

                        wrap([$class:'BuildUser']) {
                            emailext(
                                to: env.BUILD_USER_EMAIL,
                                subject: "${params.ACCION} ${params.NOMBRE} - ${params.AMBIENTE}",
                                body: salida
                            )
                        }

                        sh utils.getOcLogoutCmd()
                    }
                }
            }
        }
    }
}
